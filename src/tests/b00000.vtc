varnishtest "Test utf8.transform() with UTF-8"

server s1 {
	rxreq
	txresp
} -start

varnish v1 -vcl+backend {
	import utf8 from "${vmod_topbuild}/src/.libs/libvmod_utf8.so";

	sub vcl_deliver {
		# No options
		set resp.http.no-opt = utf8.transform(req.http.utf8, 0);
		# Case folding
		set resp.http.cf = utf8.transform(req.http.utf8, 1024);
		# Strip all markings
		set resp.http.sm = utf8.transform(req.http.utf8, 8192);
		# Case folding + strip all markings
		set resp.http.cf-sm = utf8.transform(req.http.utf8,
		    1024 + 8192);
		# Case folding + strip all markings + lump certain
		# characters together
		set resp.http.cf-sm-lc = utf8.transform(req.http.utf8,
		    1024 + 8192 + 4096);
	}
} -start

client c1 {
	txreq -hdr "utf8: áéíóúçñßÁÉÍÓÚÇÑ‐−"
	rxresp
	expect resp.http.no-opt == "áéíóúçñßÁÉÍÓÚÇÑ‐−"
	expect resp.http.cf == "áéíóúçñssáéíóúçñ‐−"
	expect resp.http.sm == "aeioucnßAEIOUCN‐−"
	expect resp.http.cf-sm == "aeioucnssaeioucn‐−"
	expect resp.http.cf-sm-lc == "aeioucnssaeioucn--"
} -run
